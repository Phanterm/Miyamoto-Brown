{
	id: "loadout_button",
	is_strict: true,
	use_absolute_screen_coordinates: true,
	auto_anchor: true,

	properties: {
		row: { type: "int" },
		col: { type: "int" },

		_cursor: { type: "null|obj button_cursor" },

		create_cursor: "def() ->commands
		execute(me,
		if(_cursor = null,
			spawn('button_cursor', {
				_width: img_w+4,
				_height: img_h+4,
				zorder: zorder-1,
			}, [
				set(_cursor, child),
				set(child.mid_x, mid_x),
				set(child.mid_y, mid_y),
			])
		)
		)
		",

		destroy_cursor: "def() ->commands execute(me, [
			remove_object(_cursor),
			set(_cursor, null),
		])",

		_time_with_focus: "int :: level.cycle - _got_focus",

		_got_focus: { type: "int", default: -1 },

		focus: {
			type: "bool",
			default: false,
			set: "if(_data != value, [
				if(value, set(_got_focus, level.cycle)),
				if(value, create_cursor(), destroy_cursor()),

				//go through all other buttons and turn off their focus.
				if(value, map(level.chars, if(value is obj loadout_button and value != me, set(value.focus, false)))),
				set(_data, value),
			])",
		},

		_control_pressed: "def(ControlKey ctrl) ->bool
			lib.game.control_pressed(obj miyamoto_playable<- level.player, ctrl)
		",

		_shift_focus: "def(int target_row, int target_col) ->commands
		[
		debug(['FOCUS', target_row, target_col, button.row, button.col]);
			set(button.focus, true)
		]
		where button = _find_focus(target_row, target_col)
		",

		_find_focus: "def(int target_row, int target_col) ->obj loadout_button

		if(exact_match != null, exact_match,
			first_row_candidate != null and last_row_candidate != null,
			if(target_col < 0, last_row_candidate, first_row_candidate),
			
			if(target_row < 0, _find_focus(last_row, target_col),
			   target_row > last_row, _find_focus(0, target_col),
			   me
			)
		)
			
			where exact_match = find(row_candidates, value.col = target_col)

			where last_row = int<- max(map(candidates, value.row))
		
			where first_row_candidate = choose(row_candidates, -value.col)
			where last_row_candidate = choose(row_candidates, value.col)
			where row_candidates = filter(candidates, value.row = target_row)
			where candidates = filter(level.chars, value is obj loadout_button)
		",
	},

	events: {
		create: "if(focus, create_cursor())",
		process: "if(_cursor, [
			set(_cursor.mid_xy, mid_xy),

			if(focus and _time_with_focus > 1, [
				if(_control_pressed(enum control_down), _shift_focus(row+1, col)),
			]),
		])",
	},

	animation: [
	{
	"@base": true,
	image: "inventory/inventory.png",
	duration: -1,
	},
	{
		id: "continue",
		rect: [0,0,47,31],
		frames: 1,
	},
	],
}
